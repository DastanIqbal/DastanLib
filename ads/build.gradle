def home = "/mnt/01D1ABAF100032D0/Iqbal/DastanLib"

apply plugin: 'com.android.library'
apply from: home + "/kotlin.gradle"
apply from: home + '/test-dependencies.gradle'

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

// This is the library version used when deploying the artifact
version = "0.1.3-alpha"

ext {
    PUBLISH_GROUP_ID = 'com.dastanapps.dastanlib'
    PUBLISH_ARTIFACT_ID = 'dastanlib'
    PUBLISH_VERSION = "$version"
}
android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
if (!rootProject.ext.devMode) {
    rootProject.ext.artifact = project.name
    apply from: '../deploy.gradle'
}

dependencies {
    api fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //AdNetwork
    api('com.facebook.android:audience-network-sdk:4.28.0') {
        transitive = true
    }
    api('com.github.vungle:vungle-android-sdk:5.3.0') {
        transitive = true
    }
    api 'com.google.android.gms:play-services-ads:18.0.0'
    api 'com.google.android.gms:play-services-measurement-base:17.0.0'
    api 'com.google.android.gms:play-services-measurement-api:17.0.0'

    api 'com.startapp:inapp-sdk:3.7.1'
    // For banners
    api('com.mopub:mopub-sdk-banner:5.0.0@aar') {
        transitive = true
        exclude module: 'moat-mobile-app-kit' // To exclude Moat
    }
    // For rewarded videos. This will automatically also include interstitials
    api('com.mopub:mopub-sdk-rewardedvideo:5.0.0@aar') {
        transitive = true
        exclude module: 'moat-mobile-app-kit' // To exclude Moat
    }

    //Analytics
    api 'com.flurry.android:analytics:6.4.2'

    //Crashlytics
    api('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true
    }
    if (!rootProject.ext.devMode) {
        api 'com.dastanapps.dastanlib:common:0.1.1.3-alpha'
    } else {
        api project(":common")
    }
}
