buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply from: '../kotlin.gradle'

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.dastanapps.dastanapps"
        minSdkVersion 16
        targetSdkVersion rootProject.ext.compileSdkVersion
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled true
    }
    dexOptions {
        preDexLibraries = false
    }

    flavorDimensions android.defaultConfig.versionName
    productFlavors {
        dev {
            dimension android.defaultConfig.versionName
            //Admob
            resValue 'string', 'admob_app_id', "ca-app-pub-3940256099942544~3347511713"
            resValue 'string', 'admob_reward_ad', "ca-app-pub-3940256099942544/5224354917"
            resValue 'string', 'admob_ads_banner', "ca-app-pub-3940256099942544/6300978111"
            resValue 'string', 'admob_ads_interstial', "ca-app-pub-8334207332200892/1601691980"

        }
    }
}

configurations {
    custom
}

apply from: '../android-support.gradle'

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')

    api 'com.mcxiaoke.volley:library:1.0.19'

    androidTestImplementation 'androidx.multidex:multidex-instrumentation:2.0.0'

    if (rootProject.ext.devMode) {
        print "Dev Mode"
        api 'com.dastanapps.dastanlib:ui:0.1.3.1-alpha'
        api 'com.dastanapps.dastanlib:common:0.2-alpha@aar'
        api 'com.dastanapps.dastanlib:ads:0.1.3-alpha'
    } else {
        api(project(path: ':ads'))
        api(project(path: ':common'))
        api(project(path: ':ui'))
    }
    //Glide
    api("com.github.bumptech.glide:glide:$glide")
    kapt "com.github.bumptech.glide:compiler:$glide"
}
